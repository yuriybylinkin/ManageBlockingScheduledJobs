
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТаблицуРегламентныхЗаданий();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаРегламентныхЗаданий

&НаКлиенте
Процедура ТаблицаРегламентныхЗаданийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Если Поле.Имя <> "ТаблицаРегламентныхЗаданийКомандаИзмененияРазрешения" Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ТаблицаРегламентныхЗаданий.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Метод = ТекущиеДанные.Метод;
	ИмяРегламентногоЗадания = ТекущиеДанные.ИмяРегламентногоЗадания;
	КомандаИзмененияРазрешения = ТекущиеДанные.КомандаИзмененияРазрешения;
	ИмяПользователя = ТекущиеДанные.ИмяПользователя;
	
	Если КомандаИзмененияРазрешения = "Убрать разрешение запуска" Тогда
		УстановкаРазрешения = Ложь;
	ИначеЕсли КомандаИзмененияРазрешения = "Установить разрешение запуска" Тогда
		УстановкаРазрешения = Истина;
	Иначе
		Возврат;
	КонецЕсли;
	
	УстановитьРазрешениеРегламентногоЗадания(Метод, УстановкаРазрешения, ИмяПользователя);
	
	СпозиционироватьсяНаРегламентномЗадании(ИмяРегламентногоЗадания);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьТаблицу(Команда)
	
	ТекущиеДанные = Элементы.ТаблицаРегламентныхЗаданий.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		ИмяРегламентногоЗадания = "";
	Иначе
		ИмяРегламентногоЗадания = ТекущиеДанные.ИмяРегламентногоЗадания;
	КонецЕсли;
	
	ЗаполнитьТаблицуРегламентныхЗаданий();
	
	Если ЗначениеЗаполнено(ИмяРегламентногоЗадания) Тогда
		СпозиционироватьсяНаРегламентномЗадании(ИмяРегламентногоЗадания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуРегламентныхЗаданий()
	
	ТаблицаРегламентныхЗаданий.Очистить();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого РегламентноеЗадание Из Метаданные.РегламентныеЗадания Цикл
		
		НоваяСтрока = ТаблицаРегламентныхЗаданий.Добавить();
		НоваяСтрока.ИмяРегламентногоЗадания = РегламентноеЗадание.ПолноеИмя();
		НоваяСтрока.Представление = РегламентноеЗадание.Представление();
		НоваяСтрока.Метод = РегламентноеЗадание.ИмяМетода;
		
		Отбор = Новый Структура("Метаданные", РегламентноеЗадание);
		Задания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
		
		Для Каждого СтрокаЗадания Из Задания Цикл
			НоваяСтрока.ИмяПользователя = СтрокаЗадания.ИмяПользователя;
			Прервать;
		КонецЦикла;
			
		НоваяСтрока.Разрешен = ОбщегоНазначения.ХранилищеСистемныхНастроекЗагрузить("РегламентныеЗадания", 
									РегламентноеЗадание.ИмяМетода, , , НоваяСтрока.ИмяПользователя);
			
		Если Не ЗначениеЗаполнено(НоваяСтрока.ИмяПользователя) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НоваяСтрока.Разрешен Тогда
			КомандаИзмененияРазрешения = "Убрать разрешение запуска";
		Иначе
			КомандаИзмененияРазрешения = "Установить разрешение запуска";
		КонецЕсли;
		
		НоваяСтрока.КомандаИзмененияРазрешения = КомандаИзмененияРазрешения;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура УстановитьРазрешениеРегламентногоЗадания(Метод, Разрешение, ИмяПользователя)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Разрешение Тогда
		ОбщегоНазначения.ХранилищеСистемныхНастроекСохранить("РегламентныеЗадания", Метод, Истина, , ИмяПользователя);
	Иначе
		ОбщегоНазначения.ХранилищеСистемныхНастроекУдалить("РегламентныеЗадания", Метод, ИмяПользователя);
	КонецЕсли;
	
	ЗаполнитьТаблицуРегламентныхЗаданий();
	
КонецПроцедуры

&НаКлиенте
Процедура СпозиционироватьсяНаРегламентномЗадании(ИмяРегламентногоЗадания)
	
	ПоискРегламентногоЗадания = Новый Структура("ИмяРегламентногоЗадания", ИмяРегламентногоЗадания);
	НайденныеСтроки = ТаблицаРегламентныхЗаданий.НайтиСтроки(ПоискРегламентногоЗадания);
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		Идентификатор = НайденнаяСтрока.ПолучитьИдентификатор();
		Элементы.ТаблицаРегламентныхЗаданий.ТекущаяСтрока = Идентификатор;
		Прервать;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти